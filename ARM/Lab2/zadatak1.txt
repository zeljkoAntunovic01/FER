ORG 0
        B MAIN
        ORG 0x18
        B NSTANJE ;skok na obradu IRQ iznimke
MAIN    ;inicijalizacija stogova
        MSR CPSR, #0b11010010 ;prelazak u IRQ mod
        MOV R13, #0x10000     ;inicijalizacija IRQ stoga
        MSR CPSR, #0b11010011 ;prelazak u svc mod
        MOV R13, #0xFC00      ;inicijalizacija svc stoga

        LDR R2, GPIO          ;adresa GPIO-a
        MOV R1, #0b11100000   ;smjer vrata A
        STR R1, [R2, #8]      ;posalji smjer u PA_DDR
        MOV R1, #0b00100000   ;pocetnje stanje = 1
        STR R1, [R2, #0]      ;pocetno stanje = crveno svjetlo

        LDR R0, RTC           ;adresa RTC-a
        MOV R1, #0
        STR R1, [R0,#0x0C]    ;brisanje brojila
        MOV R1, #5
        STR R1, [R0,#0x04]    ;upisivanje konstante u MR (5 sekundi)
        MOV R1, #1
        STR R1, [R0,#0x10]    ;dozvoli prekid RTC-u
        MRS R0, CPSR    
        BIC R0, R0, #0b10000000
        MSR CPSR, R0          ;dozvoli prekid IRQ
      
PETLJA  B PETLJA
GPIO    DW 0xFFFF0B00
RTC     DW 0xFFFF0E00
BRPAK   DW 0

NSTANJE
        STMFD SP!, {R0,R1,R2} ;spremi kontekst
        LDR R0, GPIO       ;adresa GPIO-a
        LDR R1, [R0, #0]   ;ucitaj trenutno stanje

        CMP R1, #0b00100000 ;provjeri jeli crveno (1)
        MOVEQ R2, #0b01100000 ;sljedece stanje (R2) = crveno i zuto (2)
        CMP R1, #0b01100000 ;provjeri jeli crveno i zuto (2)
        MOVEQ R2, #0b10000000 ;sljedece stanje (R2) = zeleno (3)
        CMP R1, #0b10000000 ;provjeri jeli zeleno (3)
        MOVEQ R2, #0b01000000 ;sljedece stanje (R2) = zuto (4)
        CMP R1, #0b01000000 ;provjeri jeli zuto(4)
        MOVEQ R2, #0b00100000 ;sljedece stanje (R2) = crveno (1)
        
        STR R2, [R0,#0] ;sljedece stanje = R2

        LDR R0, RTC        ;adresa RTC-a
        MOV R1, #0
        STR R1, [R0,#0x0C] ;resetiraj brojilo
        STR R1, [R0,#0x08] ;obrisi status
        
        
        LDMFD SP!, {R0,R1,R2}
        SUBS PC, LR, #4